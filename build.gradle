plugins {
    id 'groovy'
    id 'pl.allegro.tech.build.axion-release' version '1.2.0'
    id 'maven'
    id 'signing'
}



sourceCompatibility = 1.7
targetCompatibility = 1.7

scmVersion {
    tag {
        prefix = 'wiremock-groovy'
    }
}
project.version = scmVersion.version


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.0'
    compile 'org.apache.httpcomponents:httpclient:4.3.5'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'com.github.tomakehurst:wiremock:1.53'
    //for wiremock logging
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'ch.qos.logback:logback-core:1.1.2'

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def isReleaseBuild() {
    !project.version.toString().contains('SNAPSHOT')
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}